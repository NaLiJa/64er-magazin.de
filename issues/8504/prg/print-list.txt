

;print-list.prg ==0801==
   10 rem ********************************
   20 rem * rem-zeilen nicht abtippen !! *
   30 rem ********************************
   40 :
  210 rem *******************************
  240 rem *       peter      zuser      *
  250 rem *       bahnhofstr.  346      *
  260 rem *       a-8950  stainach      *
  270 rem *       ================      *
  290 rem *       tel.: 03682/2648      *
  300 rem *******************************
  310 :
63974 printchr$(147)chr$(17)
63975 printtab(11)"programm printlist"
63976 printtab(11)"=================="
63977 print
63978 printtab(14)"copyright by"
63979 printtab(12)"zuser peter 1984"
63980 fork=1to4:print:next
63981 input"ueberschrift: ";ue$
63982 an=peek(43)+256*peek(44)
63983 zn=-1:lz=63974:sz=0
63984 open1,4:print#1
63985 print#1,ue$
63986 sz=sz+1:ez=zn+1
63987 fori=1to60:zn=peek(an+2)+256*peek(an+3)
63988 ifzn=lzthenzn=zn-1:zz=i:i=61
63989 an=peek(an)+256*peek(an+1)
63990 next
63991 print#1,spc(70)"seite:";sz
63992 poke198,10
63993 forj=631to640:reada:pokej,a:next:restore
63994 printchr$(147)"cmd1:list"ez"-"zn:end
63995 fork=1to4:print#1:next
63996 oni-60goto63985
63997 forl=1to61-zz:print#1:next
63998 close1:poke198,0:end
63999 data 19,13,71,207,54,51,57,57,53,13

