<!DOCTYPE html>
<html lang="de">

<head>
	<title>Elektronisches Notizbuch</title>
	<meta charset="UTF-8">
	<link rel="stylesheet" href="../style.css">
	<meta name="author" content="Jörg Fuhr">
	<meta name="64er.issue" content="4/84">
	<meta name="64er.pages" content="50-55">
	<meta name="64er.head1" content="Superkalender">
	<meta name="64er.head2" content="VC 20+ 16 KByte">
	<meta name="64er.toc_title" content="Elektronisches Notizbuch: Mehr als nur ein Kalenderprogramm">
	<meta name="64er.toc_category" content="Programme zum Abtippen|Anwendungen">
	<meta name="64er.index_title" content="Elektronisches Notizbuch (VC 20)">
	<meta name="64er.index_category" content="Programme zum Abtippen|Anwendung|Kalender">
	<meta name="64er.id" content="kalender">
</head>

<body>
	<article>
		<h1>Elektronisches Notizbuch</h1>

		<p class="intro">Mit dem Programm Kalender können Sie sich für jeden Monat ein individuelles Datenblatt mit allen wichtigen Eintragungen erstellen lassen. Kalender läuft auf einem VC 20 mit 16 KByte Erweiterung. Zur Speicherung dient ein Kassettenrecorder; für den Ausdruck sorgt der Drucker VC 1515.</p>

		<figure>
			<img src="50-1.png" alt="">
			<figcaption>Mit diesen Menüs erfolgt die Dateneingabe für das Kalenderprogramm</figcaption>
		</figure>

		<p>Dieses Programm unterscheidet sich von herkömmlichen durch:</p>

		<ul>
			<li>Jahreszahlgroßschrift (aus Zeichengenerator-ROM)</li>
			<li>Zeilen- und Kastenkalenderausdruck des jeweils vorangegangenen und kommenden Monats</li>
			<li>Universelle Bestimmung des Wochentages vom 1.1. zur Bestimmung des Kalenderanfangs</li>
			<li>Berechnung der beweglichen Feiertage</li>
			<li>Eingabe und Ausdruck der nicht beweglichen Feiertage</li>
			<li>Eingabe und Ausdruck sonstiger beliebiger Notizen</li>
			<li>Abspeicherung und Wiedereinlesen beliebiger Notizen</li>
			<li>Möglichkeit des Inversdruckes von Wochentagen und Bemerkungen mittels Steuerkennzeichen</li>
			<li>Leichte Erweiterbarkeit (zum Beispiel Tagesnumerierung, Wochenzählung, Berechnung von Sonnenauf- und Sonnenuntergang...).</li>
		</ul>

		<p>Alles in allem ein sehr komfortables Kalenderprogramm.</p>

		<address class="author">(Jörg Fuhr)</address>

		<figure>
			<img src="50-2.png" alt="">
			<figcaption>Nassi-Shneidermann-Diagramm für das Programm "Kalender". Der Programmablaufplan ist vereinfacht wiedergegeben.</figcaption>
		</figure>
		<figure>
			<img src="50-3.png" alt="">
			<figcaption>So sind die Unterprogramme aufgebaut</figcaption>
		</figure>
		<figure>
			<img src="50-4.png" alt="">
			<figcaption>Beispiel für den Monat April</figcaption>
		</figure>

		<figure>
			<pre>
10 REM PROGRAMMVORBEREITUNGEN
20 DIMM$(13),M(12),H(13,7),S(415),N$(415)
30 GOSUB1580
40 PRINT"{CLR}{DOWN} K A L E N D E R":PRINT"{DOWN}{RVON}                      {RVOF}":PRINT:PRINT:PRINT
50 INPUT"JAHR";JA
60 INPUT"{DOWN}{DOWN}ANFANGSMONAT";AM
70 PRINT"{DOWN}ZAHL DER AUSZUDRUCKEN-DEN MONATE"
80 INPUTW
90 W=W-1
100 IFW+AM&gt;12THENPRINT"{DOWN}KALENDERAUSDRUCK GEHT NUR BIS DEZEMBER"JA:GOTO60
110 GOSUB1690:GOSUB2230
120 REM DATENEINGABE
130 PRINT"{CLR}{DOWN}DATENEINGABE{DOWN}"
140 PRINT"{RVON}                      {RVOF}"
150 PRINT"{DOWN}1 - DATENEINGABE"
160 PRINT"{DOWN}2 - ZUSAETZLICH SPEI-     CHERUNG AUF BAND"
170 PRINT"{DOWN}3 - EINLESEN VON DATEN    AUF BAND"
180 PRINT"{DOWN}4 - ENDE DER EINGABE"
190 PRINT"{DOWN}BITTE WAEHLEN"
200 GETX$:IFX$=""THEN200
210 IFVAL(X$)&lt;1ORVAL(X$)&gt;5THEN200
220 ONVAL(X$)GOTO230,280,420,840
230 REM DATENEINGABE NUR IN ARBEITSSPEICHER
240 GOSUB720
250 IFFD=1THEN120
260 GOSUB530
270 GOTO230
280 REM DATENEINGABE + ABSPEICHERUNG
290 PRINT"{DOWN}FILENAME"
300 INPUT NF$
310 OPEN2,1,1
320 PRINT#2,NF$
330 GOSUB720
340 PRINT#2,TM
350 IFFD=1THENCLOSE2:GOTO120
360 PRINT#2,X$+E$
370 GOSUB530
380 IFDE=0THEN410
390 PRINT"NEUE EINGABE WIRD     DENNOCH ABGESPEICHERT!"
400 FORI=1TO3000:NEXT
410 GOTO330
420 REM DATEN VOM BAND EINLESEN
430 PRINT"{DOWN}FILENAME"
440 INPUTNF$
450 OPEN2,1,0
460 INPUT#2,X$
470 IFNF$&lt;&gt;X$THEN450
480 INPUT#2,TM
490 IFTM=0THENCLOSE2:GOTO120
500 INPUT#2,E$
510 GOSUB530
520 GOTO480
530 REM DOPPELEINTRAG-PRUEFUNG  UP 7
540 DE=0
550 IFN$(TM)=""THENN$(TM)=E$:RETURN
560 M=INT(TM/32):T=TM-32*M
570 PRINT"{CLR}{DOWN}DOPPELEINTRAG FEST-   GESTELLT AM"
580 PRINTT"."M"."JA
590 PRINT"{RVON}                      {RVOF}"
600 PRINT"{DOWN}ALTE EINGABE (1):{DOWN}"
610 PRINTN$(TM)
620 PRINT"{DOWN}{DOWN}NEUE EINGABE (2):{DOWN}"
630 PRINTE$
640 PRINT"{DOWN}{DOWN}WELCHE EINGABE SOLL   UEBERNOMMEN WERDEN 1/2"
650 GETX$:IFX$=""THEN650
660 IFX$="1"THENDE=1:RETURN
670 IFX$="2"THENN$(TM)=E$:RETURN
680 GOTO650
690 REM FEHLERMELDUNG
700 PRINT"{DOWN}{DOWN}{DOWN}{DOWN}EINGABE- F E H L E R"
710 FORI=1TO3000:NEXT
720 REM DATENEINGABE  UP 6
730 FD=0:X$=""
740 PRINT"{CLR}{DOWN}DATENEINGABE ENDE-&gt;0,0"
750 PRINT"{RVON}                      {RVOF}"
760 PRINT"TAG UND MONAT DURCH   KOMMA GETRENNT EIN-   GEBEN."
770 INPUTT,M:TM=M*32+T
780 IFT=0ANDM=0THENFD=1:E$="":RETURN
790 IFM&lt;1ORM&gt;12ORT&lt;1ORT&gt;M(M)THEN690
800 PRINT"{DOWN}{DOWN}{DOWN}EINZUTRAGENDE NOTIZ   BIS 50 ZEICHEN"
810 INPUTE$
820 IFLEFT$(E$,1)="*"THENE$=RIGHT$(E$,LEN(E$)-1):S(TM)=1
830 RETURN
840 REM KALENDERAUSDRUCK
850 PRINT"{UP}  W  A  I  T       "
860 POKE36879,8
870 GOSUB1930
880 IFAM=1THENJA=JA-1:AM=13:AJ=1
890 IFAM=12THENZJ=1
900 M=1:T=1
910 GOSUB2100
920 IFM&lt;&gt;AM-1THENGOSUB2270:GOTO920
930 POKE36879,27
940 PRINT"{UP}  DRUCKVORGANG"
950 REM KASTENKALENDER
960 FORX=1TO13
970 FORY=1TO7
980 H(X,Y)=0
990 NEXTY,X
1000 T=1:X=1
1010 FORY=WTTO7
1020 H(X,Y)=T
1030 T=T+1
1040 IFT&gt;M(M)THEN1080
1050 NEXT
1060 WT=1:X=X+1
1070 GOTO1010
1080 IF FLTHEN1170
1090 IFAJTHENJA=JA+1:GOSUB2230
1100 X=X+2:WT=Y+1:M=M+1:TW=WT
1110 IFM=13THENM=1:AM=1
1120 WT=M(M)-7*INT(M(M)/7)+WT
1130 IFWT&gt;7THENWT=WT-7
1140 T=1:M=M+1:FL=1
1150 IFM=13THENM=1
1160 GOTO1010
1170 REM AUSDRUCK KASTENKALENDER
1180 OPEN1,4
1190 PRINT#1," "SPC(23)M$(AM-1)JA-AJ;SPC(4)M$(AM+1)JA+ZJ
1200 FL=0:AJ=0:ZJ=0
1210 FORY=1TO7
1220 PRINT#1,"{$A0}"SPC(10)T$(Y)"  ";
1230 FORX=1TO13
1240 X$=STR$(H(X,Y))
1250 IFX$=" 0"THENX$="{$A0}{$A0}"
1260 IFH(X,Y)&lt;10THENX$=" "+X$
1270 PRINT#1,X$;
1280 NEXTX
1290 PRINT#1
1300 NEXTY
1310 PRINT#1:PRINT#1:PRINT#1:CLOSE1
1320 WT=TW:T=1:M=AM:GOSUB2320
1330 REM MONAT AUSDRUCKEN
1340 OPEN1,4
1350 PRINT#1,CHR$(14)M$(M):PRINT#1,"= = = = =":PRINT#1,CHR$(15)
1360 IFT&lt;10THENTT$=" "+STR$(T)+". "
1370 IFT&gt;9THENTT$=STR$(T)+". "
1380 TH$=T$(WT)
1390 IFS(M*32+T)=1THENTH$="{RVON}"+TH$+"{RVOF}"
1400 PRINT#1,CHR$(15)TH$;TT$;M$(M);
1410 S$=N$(M*32+T):X=LEN(S$)
1420 IFX=0THENS$=".  .  .  .  .  .  .  .  .  .  .  .  .  .  .  .  .  .":GOTO1470
1430 S$=S$+"   ":S$=LEFT$(S$,3+3*INT(X/3)):X=LEN(S$)
1440 FORI=XTO51STEP3
1450 S$=S$+".  "
1460 NEXT
1470 PRINT#1,S$;CHR$(8)
1480 PRINT#1,CHR$(8)
1490 GOSUB2270
1500 IFM&lt;AM+1THEN1360
1510 FORI=M(M-1)TO37
1520 PRINT#1
1530 NEXT
1540 REM WIEDERHOLUNG
1550 CLOSE1
1560 IFW&gt;0THENAM=AM+1:W=W-1:GOTO840
1570 PRINT"{CLR}":END
1580 REM FELDER EINLESEN  UP 1
1590 FORI=1TO7:READF$(I):NEXT
1600 FORI=1TO7:READF(I):NEXT
1610 READT,M,E$
1620 IFLEFT$(E$,1)="*"THENE$=RIGHT$(E$,LEN(E$)-1):S(M*32+T)=1
1630 N$(M*32+T)=E$
1640 IFT&gt;0ANDM&gt;0THEN1610
1650 FORI=1TO7:READT$(I):NEXT:REM WOCHENTAGE EINLESEN
1660 FORI=0TO13:READM$(I):NEXT:REM MONATSNAMEN EINLESEN
1670 FORI=1TO12:READM(I):NEXT
1680 RETURN
1690 REM FEIERTAG-BERECHNUNG  UP 2
1700 IFJA&lt;1800THENRETURN
1710 IFJA&gt;=1800THENR=23:S=4
1720 IFJA&gt;=1900THENR=24:S=5
1730 IFJA&gt;=2100THENR=24:S=6
1740 IFJA&gt;=2200THENR=25:S=0
1750 IFJA&gt;=2300THENRETURN
1760 F1=R+19*(JA-19*INT(JA/19))
1770 F=F1-30*INT(F1/30)
1780 G1=JA-4*INT(JA/4)
1790 G2=JA-7*INT(JA/7)
1800 G3=2*G1+4*G2+6*F+S
1810 G=F+G3-7*INT(G3/7)
1820 IFG=35THENG=28
1830 IFG=34ANDF=28ANDJA-19*INT(JA/19)&gt;10THENG=27
1840 FORI=1TO7
1850 X=G-F(I)
1860 IFX&lt;=1THENX=X+30.9
1870 IFX&lt;=1THENX=X+29.9
1880 X=320*X-319*INT(X)
1890 X=INT(X+0.5)
1900 N$(X)=F$(I):S(X)=1
1910 NEXTI
1920 RETURN
1930 REM JAHRESZAHLAUSDRUCK  UP 3
1940 ZA=32768:POKEZA,240
1950 OPEN1,4:PRINT#1,CHR$(14);
1960 FORJ=1TO4
1970 Z=VAL(MID$(STR$(JA),J+1,1))
1980 ZZ(J)=ZA+8*(48+Z)
1990 NEXTJ
2000 FORI=0TO7:REM ZEILE
2010 PRINT#1:PRINT#1,"   ";
2020 FORJ=1TO4:REM ZAHL
2030 FORK=7TO0STEP-1:REM SPALTE
2040 IF2^K AND PEEK(ZZ(J)+I)THENPRINT#1,"{CBM-+}";:GOTO2060
2050 PRINT#1," ";
2060 NEXTK,J,I
2070 PRINT#1,CHR$(15):PRINT#1:PRINT#1:PRINT#1
2080 CLOSE1
2090 RETURN
2100 REM BERECHNUNG WOCHENTAG 1.JANUAR  UP 4
2110 X1=JA-28*INT(JA/28)
2120 IFX1=0THENX1=28
2130 IFX1&gt;14.5THENX2=X1-14:GOTO2150
2140 ONX1GOTO2220,2160,2170,2180,2200,2210,2220,2160,2180,2190,2200,2210,2160,2170
2150 ONX2GOTO2180,2190,2210,2220,2160,2170,2190,2200,2210,2220,2170,2180,2190,2200
2160 WT=1:GOTO2230
2170 WT=2:GOTO2230
2180 WT=3:GOTO2230
2190 WT=4:GOTO2230
2200 WT=5:GOTO2230
2210 WT=6:GOTO2230
2220 WT=7:GOTO2230
2230 REM SCHALTJAHRBERECHNUNG
2240 M(2)=28
2250 IFJA/4&lt;&gt;INT(JA/4)THENRETURN
2255 IFJA/100&lt;&gt;INT(JA/100)OR(JA/100=INT(JA/100)ANDJA/400=INT(JA/400))THENM(2)=M(2)+1
2260 RETURN
2270 REM TAG-MONAT-BERECHNUNG  UP 5
2280 REM WT+T AUS UP
2290 IFT=M(M)THENT=0:M=M+1
2300 T=T+1
2310 WT=WT+1
2320 IFWT=8THENWT=1
2330 RETURN
2340 DATAOSTERN,KARFREITAG,OSTERMONTAG,HIMMELFAHRT,PFINGSTEN,PFINGSTMONTAG,FRONLEICHNAM
2350 DATA8.6,10.6,7.6,30.4,20.4,19.4,9.4
2360 DATA1,1,*NEUJAHR,1,5,*MAIFEIERTAG,17,6,*GESETZL.FEIERTAG,1,11,*ALLERHEILIGEN
2370 DATA24,12,HEILIG ABEND,25,12,*1.WEIHNACHTSTAG,26,12,*2.WEIHNACHTSTAG
2380 DATA21,3,FRUEHLINGSANFANG,22,6,SOMMERANFANG,23,9,HERBSTANFANG
2390 DATA22,12,WINTERANFANG,0,0,
2400 DATAMONTAG    ,DIENSTAG  ,MITTWOCH  ,DONNERSTAG
2410 DATA FREITAG   ,SAMSTAG   ,"{RVON}SONNTAG   {RVOF}"
2420 DATA DEZEMBER ,JANUAR   ,FEBRUAR  ,MAERZ    ,APRIL    ,MAI      ,"JUNI     "
2430 DATA JULI     ,AUGUST   ,SEPTEMBER,OKTOBER  ,NOVEMBER ,DEZEMBER ,"JANUAR   "
2440 DATA 31,28,31,30,31,30,31,31,30,31,30,31
</pre>
			<figcaption>Listing des Programms »Kalender«</figcaption>
		</figure>

	</article>

	<aside class="downloads">
		<a href="prg/kalender.prg">Kalender</a>
	</aside>
</body>

<!--
Das Listing "Kalender" heißt in 84XX/8404.D64 auf der 64'er CD-ROM
"VC20ELEKTR.NOTIZ".
-->

</html>
