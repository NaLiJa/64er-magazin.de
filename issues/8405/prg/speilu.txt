;speilu.prg ==0801==
;Dies ist die vollständige Version des Programms.
; * In 4/84 ist eine Version ohne die "Ändern eines Zeichens" Funktion abgedruckt.
; * In 5/84 sind Patches zur vollständigen Version

    1 rem**************************************************************************
    2 rem*                                                                        *
    3 rem*                                                                        *
    4 rem*                             s p e i l u                                *
    5 rem*                                                                        *
    6 rem*                                                                        *
    7 rem*   manfred thoma  +  heimo ponnath  2102 hamburg 93  veringstrasse 82   *
    8 rem*                                                                        *
    9 rem**************************************************************************
   10 poke52,48:poke56,48:poke53280,6:poke53281,6:poke646,1
   20 te$=" adresse  dez. hexa   binaer    grafik"
  100 printchr$(147)"  darstellung von zeichen und speicher"
  105 print:printchr$(18);tab(10);"====================="
  110 printchr$(18);tab(10)"thoma/ponnath hamburg"
  115 printchr$(18);tab(10);"====================="
  120 print:print:print"     (1) einsehen in einen speicher"
  130 print:print"     (2) darstellung eines zeichens"

; *** neu
  140 print:print"     (3) aendern eines zeichens"
  145 print:print"     (4) programm-ende"
; *** neu
  
  150 print:print:print"       bitte kennziffer waehlen !"
  160 geta$:ifa$=""then160

; *** geändert
  170 a=val(a$):ifa<0ora>4then160
  180 onagosub1000,2000,3000,4000
; *** geändert
  
  190 goto100
 1000 printchr$(147);chr$(18);tab(2);"darstellung eines speicherplatzes:"
 1010 print:print"  speicheradresse (0-65535) eingeben"
 1020 input"adresse :";ad:ifad<0orad>65535thenreturn
 1040 de=peek(ad):print:print:gosub10000:gosub20000:printte$:print:gosub30000
 1050 print:print:goto1010
 2000 printchr$(147);chr$(18);tab(3);"zeichen-darstellung (character)"
 2010 ifts=0thengosub40000
 2020 print:print"     gebe den 'bildschirm code' des"
 2030 print"      darzustellenden zeichens ein"
 2040 print"   = siehe handbuch seite 133 - 134 ="
 2050 print:print"  zurueck mit zahl ausserhalb 0 und 511":print
 2060 print,:input"code :";a:ifa<0ora>511thenreturn
 2070 print:printte$:print
 2080 forad=12288+8*ato12288+8*a+7
 2100 de=peek(ad):gosub10000:gosub20000:gosub30000:nextad
 2110 geta$:ifa$=""then2110
 2120 return

 ; *** neu
 3000 printchr$(147);chr$(18);tab(1);"aendern von zeichen"
 3010 print:print"  es koennen zeichen geaendert werden"
 3020 print"indem der 'bildschirm-code' des zeichens";
 3030 print"     eingegeben wird. ( 0 - 511)":ifts=0thengosub40000
 3035 print:print"     aendern sie die binaerzahl mit"
 3037 print"'0' oder '1' entsprechend. dann <return>"
 3040 print:input"code : ";a:ifa<0ora>511thenreturn
 3050 printchr$(147):forad=12288+8*ato1228+8*a+7
 3060 de=peek(ad):gosub10000:gosub20000:gosub30000:nextad
 3070 printchr$(19):ad=ad-9
 3080 forq=1to8:printtab(19);:inputa$:ad=ad+1:gosub50000:gosub10000:gosub20000
 3090 printchr$(145);"                                       "
 3100 printchr$(145);chr$(145):gosub30000:pokead,de:nextq
 3110 geta$:ifa$=""then3110
 3120 return
 ; *** neu
 
 ; *** neu; fehlte im heft-listing
 4000 end
 ; *** neu; fehlte im heft-listing

 10000 he$="":h$="0123456789abcdef":d=int(de/16):he$=mid$(h$,d+1,1):d=de-d*16
 10010 he$=he$+mid$(h$,d+1,1):return
 20000 bi$="":di=de
 20010 di=di/2:d$="0":ifdi<>int(di)thend$="1"
 20020 di=int(di):bi$=d$+bi$:ifdi>0then20010
 20030 iflen(bi$)<8thenbi$="0"+bi$:goto20030
 20040 return
 30000 printtab(7-len(str$(ad)))ad;tab(13-len(str$(de)))de;tab(16)he$;tab(21)bi$;
 30010 fori=1to8:w$=mid$(bi$,i,1)
 30020 ifw$="1"thenprinttab(30+i)chr$(18);" ";chr$(146);:goto30040
 30030 printtab(30+i)".";
 30040 nexti
 30050 print: return
 40000 print:print"kopieren der zeichen ins ram (ab 12288)"
 40010 print"            bitte warten"
 40020 poke56334,peek(56334)and254:poke1,peek(1)and251
 40030 fori=0to4095:poke12288+i,peek(53248+i):nexti
 40040 poke1,peek(1)or4:poke56334,peek(56334)or1
 40050 poke53272,(peek(53272)and240)+12:ts=1:return

 ; *** neu
 50000 de=0:fori=1to8:ifmid$(a$,i,1)="1"thende=de+2^(8-i):
 50010 nexti:return
 ; *** neu
