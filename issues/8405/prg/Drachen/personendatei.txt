   10 rem*********************************
   20 rem*********************************
   30 rem*****      arne weitzel      ****
   40 rem*****                        ****
   50 rem***** ritter-von-halt-str.17 ****
   60 rem*****                        ****
   70 rem*****    5608 radevormwald   ****
   80 rem*********************************
   90 rem*********************************
  100 dim f$(100,11),su$(11),na$(11):poke 788,52
  110 si=54272:fl=si:fh=si+1:tl=si+2:th=si+3:ww=si+4:aa=si+5:hh=si+6:vl=si+24
  120 na$(1)="Name":na$(2)="Vorname":na$(3)="Geburtstag":na$(4)="Geburtsmonat"
  130 na$(5)="Geburtsjahr":na$(6)="Strasse":na$(7)="Hausnummer"
  140 na$(8)="Postleitzahl":na$(9)="Wohnort":na$(10)="Vorwahl":na$(11)="Telefon"
  150 rem********* m e n u e *************
  160 poke 198,0:print chr$(142):poke 53280,12:poke 53281,3
  170 print "{clr}{down}{down}"tab(11)"{blu}UCCCCCCCCCCCCCCCI"
  180 print tab(11)"B"tab(27)"B"
  190 print tab(11)"JCCCCCCCCCCCCCCCK"
  200 print"{home}{down}{down}{down}"tab(13)"personendatei"
  210 print tab(7)"{down}{down}{down}{blu}UCCCCCCCCCCCCCCCCCCCCCCCCI"
  220 for i=1 to 13:print tab(7)"B"tab(32)"B":next i
  230 print tab(7)"JCCCCCCCCCCCCCCCCCCCCCCCCK"
  240 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}"tab(9)"{gry1}ausgaben          : {lblu}f1"
  250 print tab(9)"{down}{gry1}eingabe           : {lblu}f3"
  260 print tab(9)"{down}{gry1}suchen            : {lblu}f5"
  270 print tab(9)"{down}{gry1}daten einlesen    : {lblu}f7"
  280 print tab(9)"{down}{gry1}daten speichern   : {lblu}f2"
  290 print tab(9)"{down}{gry1}datei vorbereiten : {lblu}f4"
  300 print tab(9)"{down}{down}{down}{gry1}ende              : {lblu}f6"
  310 get e$:if e$="" then 310
  320 if(asc(e$)<133) or (asc(e$)>139) then 310
  330 mo=asc(e$)-131:on mo goto 170,450,360,1180,990,1050,2020,340
  340 print"{clr}";:clr:restore:poke 53280,14:poke 53281,6:end
  350 rem********** eingabe *************
  360 mo$="EINGABE :":f3=151:f4=151:poke 53280,15:poke 53281,12:print chr$(f3)
  370 print"{clr}":gosub 830:print chr$(f4):gosub 970
  380 for j=1 to 11:gosub 1860:sys 58640
  390 gosub 680:f$(an+1,j)=a$:next:an=an+1
  400 gosub1110: gosub 1640: print:print"{down}{down}{down}{down}{rght}{rght}{cyn}Weitere Eingabe (-/n)";
  410 getw$:ifw$=""then410
  420 ifw$="n"then160
  430 goto 360
  440 rem********** ausgabe *************
  450 poke 53280,14:poke 53281,14
  460 print"{clr}{down}{down}{down}{down}{down}{down}{down}{down}{lblu}"tab(15)"{wht}ausgabe :{gry1}"
  470 print tab(10)"{down}f1 - sortiert"
  480 print tab(10)"{down}f3 - physikalisch"
  490 get au$:if (au$="") or (au$<>"{f1}" and au$<>"{f3}") then 490
  500 z=0:gosub 2000
  510 if au$="{f1}" then 590
  520 z=z+1:if z>an then gosub 610:goto 160
  530 print "{clr}":if f$(z,0)="@" then 520
  540 gosub 630:gosub 1640
  550 print"{down}{down}{down}{down}{rght}{rght}{gry3}Weitere Ausgaben ? (-/n)";
  560 get w$:if w$="" then 560
  570 if w$="n" then 160
  580 goto 520
  590 ma=z:z=val(f$(z,0)):if z=0 then gosub 610:goto 160
  600 print"{clr}":gosub 630:goto 1490
  610 print"{clr}{down}{down}{down}{down}{down}{down}{rght}{rght}{gry3}Keine weiteren Personen erfasst !":gosub 1640
  620 for i=1 to 3000:next:return
  630 poke 53280,f1:poke 53281,f2:print chr$(f3):gosub 830
  640 print "{home}"tab(25)"{down}{lred}Datensatz "z;chr$(f4)
  650 for j=1 to 11:gosub 1860:sys 58640
  660 print f$(z,j):next j:return
  670 rem************ input ************
  680 ll=0:a$="":poke 204,0
  690 get aa$:if aa$="" then 690
  700 if asc(aa$)<>13 then 730
  710 if ll=0 then 690
  720 print "{CBM-@}";:poke 204,1:return
  730 if asc(aa$)<>20 then 760
  740 if ll=0 then 690
  750 print "{CBM-@}{left}{left}{CBM-@}{left}";:a$=left$(a$,len(a$)-1):ll=ll-1:goto 690
  760 if aa$<>"{f7}" then 780
  770 goto 800
  780 print aa$;:a$=a$+aa$:ll=ll+1
  790 if ll<l then 690
  800 if peek(207) then 800
  810 poke 204,1:return
  820 rem********** 1.maske **************
  830 print chr$(14):print"{home}{down}{rght}{rght}{rvon}"mo$
  840 print"{down}{rght}{rvon}";:for i=1 to 35:print" ";:next i:print
  850 for i=1 to 5:print tab(1)"{rvon} "tab(35)" ":next i
  860 print"{rght}{rvon}";:for i=1 to 35:print" ";:next i
  870 print"{home}{rvof}{down}{down}{down}{down}{down}{rght}{rght}{rght}Name    :":print"{down}{rght}{rght}{rght}Vorname :"
  880 print"{down}{down}{down}{rght}Geboren :{rght}{rght}{rght}.{rght}{rght}."
  890 print"{down}{rght}{rvon}Wohnhaft :"
  900 print"{down}{rght}Str.:";
  910 print tab(27)"Nr.:"
  920 print"{down}{rght}Plz :";
  930 print tab(13)"Ort :"
  940 print"{down}{rght}Telefon :{rght}{rght}{rght}{rght}{rght}{rght}/"
  950 return
  960 rem********* 2.maske **************
  970 for j=1 to 11:gosub 1860:sys 58640:for i=1 to l:print"{CBM-@}";:next i,j:return
  980 rem********** einlesen ************
  990 print"{clr}{down}{down}"tab(15)"{rvon}{gry1} einlesen ":i=0:open 1,8,2,"adr.-datei,s,r"
 1000 for j=0 to 11
 1010 input#1,f$(i,j)
 1020 next:if st<>64 then i=i+1:goto 1000
 1030 close 1:an=i:goto 160
 1040 rem*********** speichern *********
 1050 print"{clr}{down}{down}"tab(15)"{rvon}{gry1} speichern ":open 1,8,2,"@:adr.-datei,s,w"
 1060 for i=0 to an
 1070 for j=0 to 11
 1080 print#1,f$(i,j)
 1090 next j,i:close 1:goto 160
 1100 rem********* einsortieren ********
 1110 ma=0:vz=val(f$(0,0)):zz =vz
 1120 if vz=0 then 1160
 1130 mb=ma:ma=vz:vz=val(f$(ma,0))
 1140 if(f$(ma,1)<f$(an,1))or((f$(ma,1)=f$(an,1))and(f$(ma,2)<f$(an,2)))then1120
 1150 f$(an,0)=str$(ma):f$(mb,0)=str$(an):return
 1160 f$(an,0)="0":f$(ma,0)=str$(an):return
 1170 rem********** suchen *************
 1180 poke 53280,14:poke 53281,7:printchr$(14)
 1190 print"{clr}";:gosub 1990
 1200 print"{down}{blu}{rvon}                SUCHEN :                "
 1210 print"{down}{rght}{rght}{gry1}Wonach soll gesucht werden ?{down}{down}"
 1220 for j=1 to 11:print"{rght}{rght}- ";na$(j)tab(20)":";
 1230 if j=10 then print" 0":goto 1260
 1240 if j=11 then print" +":goto 1260
 1250 print j
 1260 next j
 1270 get e$:if e$="" then 1270
 1280 if (asc(e$)<48 or asc(e$)>57) and (e$<>"+") then 1270
 1290 if e$="0" then su=10:goto 1320
 1300 if e$="+" then su=11:goto 1320
 1310 su=asc(e$)-48
 1320 j=su:gosub 1860
 1330 print"{clr}{down}{down}{down}{rght}{rght}{gry1}"na$(su)
 1340 input"{home}{down}{down}{down}{down}{down}{rght}{rght}";su$(su):if len(su$(su))=0 then 1340
 1350 if len(su$(su))>1 then gosub 1370:goto 1340
 1360 goto 1400
 1370 gosub 1640:for i=1 to 4
 1380 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{rght}{rght}EINGABE ZU LANG !":for j=1 to 200:next j
 1390 print"{up}{rght}{rght}                 ":for j=1 to 200:next j,i:return
 1400 gf=0:z=0
 1410 if val(f$(z,0))<>0 then goto 1460
 1420 ifgf=0thenprint"{clr}{down}{down}{down}{down}{down}{down}{rght}{gry1}Keine entsprechenden Personen erfasst !":goto 1450
 1430 print"{clr}{down}{down}{down}{down}{down}{down}{rght}{rght}{gry3}Weitere, fuer das Suchkriterium         {down}zutreffende";
 1440 print"{rght}Personen nicht erfasst !"
 1450 gosub 1640:for i=1 to 5000:next i:goto 160
 1460 ma=z:z=val(f$(z,0))
 1470 if f$(z,su)<>su$(su) then 1410
 1480 print"{clr}":gosub 630:gf=1
 1490 gosub 1640:print"{down}{rght}{rght}Weiter"m2$;tab(18)" - f1"
 1500 print"{rght}{rght}Ende "m1$;tab(18)" - f3"
 1510 print"{rght}{rght}Loeschen"tab(18)" - f5"
 1520 print"{rght}{rght}Aendern"tab(18)" - f7";
 1530 get w$:if w$="" then 1530
 1540 la=asc(w$)-132: if (la<1) or (la>4) then 1530
 1550 if mo=2 then on la goto 590,160,1590,1730
 1560 on la goto 1410,160,1580,1720
 1570 rem********** loeschen ***********
 1580 print"{clr}{down}{down}{down}{rght}{rght}{gry3}{rvon}Loeschen :":print"{down}{down}{down}{rght}{rght}Datensatz "z" : ";f$(z,1)" , "f$(z,2)
 1590 z1$=f$(z,0):f$(z,0)="@"
 1600 f$(ma,0)=z1$
 1610 for w=1 to 3000:next
 1620 z=ma:goto 1800
 1630 rem************** ton ************
 1640 poke vl,15
 1650 poke aa,2*16+3
 1660 poke tl,0:poke th,8
 1670 poke hh,15*16+2
 1680 poke fh,62:poke fl,5
 1690 poke ww,65:for i=1 to 100:next i:poke ww,64
 1700 return
 1710 rem********** aendern ************
 1720 mo$="AENDERN :":f1=9:f2=9:f3=155:f4=5
 1730 print"{clr}{CBM-+}":gosub 970:gosub 630:print tab(8)"{down}{down}{cyn}+{gry1}keine Aenderungen - f7{rvof}+"
 1740 poke 198,0
 1750 for j=3 to 11:gosub 1860:sys58640:gosub 680:if aa$<>"{f7}" then f$(z,j)=a$
 1760 next j
 1770 print:gosub 1640:print"{down}{down}{down}{down}{rght}{rght}Aenderungen Korrekt ? (-/n)";
 1780 get w$:if w$="" then 1780
 1790 if w$="n" then 1730
 1800 print"{clr}{down}{down}{down}{down}{down}{down}{rght}{rght}{cyn}Weiter"m2$" (-/n)"
 1810 get w$:if w$="" then 1810
 1820 if w$="n" then 160
 1830 if mo=4 then gosub 1990:goto 1410
 1840 gosub 2000:goto 610
 1850 rem********* print at ************
 1860 on j goto 1870,1880,1890,1900,1910,1920,1930,1940,1950,1960,1970
 1870 l=21:sp=13:ze=5 :goto 1980
 1880 l=21:sp=13:ze=7 :goto 1980
 1890 l=2 :sp=11:ze=11:goto 1980
 1900 l=2 :sp=14:ze=11:goto 1980
 1910 l=4 :sp=17:ze=11:goto 1980
 1920 l=18:sp=7 :ze=15:goto 1980
 1930 l=4 :sp=33:ze=15:goto 1980
 1940 l=5 :sp=7 :ze=17:goto 1980
 1950 l=18:sp=19:ze=17:goto 1980
 1960 l=5 :sp=11:ze=19:goto 1980
 1970 l=6 :sp=17:ze=19:goto 1980
 1980 poke214,ze:poke 211,sp:return
 1990 mo$="SUCHEN :":ml$="Suchen ":m2$="suchen" :f1=14:f2=6:f3=155:f4=158:return
 2000 mo$="AUSGABE :":ml$= "Ausgaben": m2$= "e Ausgaben":f1=14:f2=11:f3=155
 2010 f4=5:return
 2020 print"{clr}{down}{down}"tab(10)"{gry1}{rvon} datei vorbereiten "
 2030 f$(0,0)="0":for j =1 to 11:f$(0,j)="-":next j
 2040 open 1,8,2,"adr.-datei,s,w"
 2050 for j=0 to 11
 2060 print#1,f$(0,j)
 2070 next j:close 1:goto 160
